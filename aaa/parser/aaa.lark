// Grammar for the
//
//    mm
//    ##    mmm    mmm
//   #  #  "   #  "   #
//   #mm#  m"""#  m"""#
//  #    # "mm"#  "mm"#
//
// language.

%import common.WS
%ignore WS

COMMENT: "//" /[^\n]*/ "\n"
%ignore COMMENT

SHEBANG: "#!" /[^\n]*/ "\n"
%ignore SHEBANG

identifier.0: /[a-zA-Z_]+/

// same as identifier, but does not disallow builtin names / keywords as values
member_function_name: /[a-zA-Z_]+/

integer: /[0-9]+/

%import common.ESCAPED_STRING
string: ESCAPED_STRING

// block delimiters
BEGIN:       /{(?=(\W|\s))/
_END:        /}(?=(\W|\s|$))/

// ignored keywords
_AS:          /as(?=(\W|\s))/
_ARGS:        /args(?=(\W|\s))/
BUILTIN_FN:   /builtin_fn(?=(\W|\s))/
BUILTIN_TYPE: /builtin_type(?=(\W|\s))/
ELSE:         /else(?=(\W|\s))/
FN:           /fn(?=(\W|\s))/
FROM:         /from(?=(\W|\s))/
_IMPORT:      /import(?=(\W|\s))/
IF:           /if(?=(\W|\s))/
_RETURN:      /return(?=(\W|\s))/
STRUCT:       /struct(?=(\W|\s))/
WHILE:        /while(?=(\W|\s))/

// keywords for builtin types
BOOL:       /bool(?=(\W|\s))/
INT:        /int(?=(\W|\s))/
MAP:        /map(?=(\W|\s))/
STR:        /str(?=(\W|\s))/
VEC:        /vec(?=(\W|\s))/

// keywords for builtin constants
FALSE:      /false(?=(\W|\s))/
TRUE:       /true(?=(\W|\s))/

// keywords for builtin boolean operations
AND:        /and(?=(\W|\s))/
NOT:        /not(?=(\W|\s))/
OR:         /or(?=(\W|\s))/

// keywords for builtin stack operations
DUP:        /dup(?=(\W|\s))/
DROP:       /drop(?=(\W|\s))/
OVER:       /over(?=(\W|\s))/
ROT:        /rot(?=(\W|\s))/
SWAP:       /swap(?=(\W|\s))/

// keywords for builtin misscellaneous operations
ASSERT:     /assert(?=(\W|\s))/
NOP:        /nop(?=(\W|\s))/

// --- builtin file rules ---

builtins_file_root: (builtin_function_declaration | builtin_type_declaration)+

builtin_type_declaration: BUILTIN_TYPE _type_literal

builtin_function_declaration: ( \
    BUILTIN_FN \
    builtin_function_name \
    [function_arguments] \
    [function_return_types] \
)

builtin_function_name: ( \
    (operator | identifier | builtin_type) \
    [type_params] \
    [":" member_function_name] \
)

// TODO replace ? with [] in this file

// --- regular source file + imports ---

regular_file_root: (function_definition | import_statement | struct_definition)+

import_statement: FROM string _IMPORT import_items
import_items: import_item ("," import_item)* ","?
import_item: identifier (_AS identifier)?

// --- structs ---

struct_definition: ( \
    STRUCT identifier \
    BEGIN arguments \
    _END \
)

struct_function_identifier: _type_literal ":" member_function_name

struct_field_query: string struct_field_query_operator
!struct_field_query_operator: "?"

struct_field_update: string BEGIN function_body _END struct_field_update_operator
!struct_field_update_operator: "!"

member_function_literal: _type_literal ":" member_function_name

// --- functions, arguments, return types ---

function_definition: ( \
    FN function_name \
    type_params? \
    function_arguments? \
    function_return_types? \
    BEGIN function_body _END \
)

function_name: struct_function_identifier | identifier

function_arguments: _ARGS arguments
arguments: argument ("," argument)* ","?
argument: identifier _AS _type_literal

function_return_types: _RETURN return_types
return_types: _type_literal ("," _type_literal)* ","?

// --- type literals ---

builtin_type.1: BOOL | INT | MAP | STR | VEC
_type_literal: (builtin_type | identifier) type_params?
type_params: "[" _type_literal ("," _type_literal)* ","? "]"

// --- literals ---

literal: boolean | integer | string
!boolean.1: TRUE | FALSE

// --- function body ---

function_body: function_body_item+

function_body_item: ( \
      member_function_literal
    | branch
    | loop
    | operator
    | (identifier type_params?)
    | (builtin_type type_params?)
    | struct_field_query
    | struct_field_update
    | literal \
)

// --- branch ---

branch: ( \
    branch_condition \
    branch_if_body \
    branch_else_body? \
)

branch_condition: IF function_body
branch_if_body: BEGIN function_body _END
branch_else_body: ELSE BEGIN function_body _END

// --- loop ---

loop: loop_condition BEGIN loop_body _END
!loop_condition: WHILE function_body
loop_body: function_body

// --- operators ---

!operator.1:  AND
            | ASSERT
            | DROP
            | DUP
            | NOP
            | NOT
            | OR
            | OVER
            | ROT
            | SWAP
            | /-(?=\s)/
            | /!=(?=\s)/
            | /\.(?=\s)/
            | /\*(?=\s)/
            | /\/(?=\s)/
            | /%(?=\s)/
            | /\+(?=\s)/
            | /<=(?=\s)/
            | /=(?=\s)/
            | />=(?=\s)/
            | /<(?=\s)/
            | />(?=\s)/

keyword:  boolean
        | operator
        | _ARGS
        | _AS
        | BEGIN
        | ELSE
        | _END
        | IF
        | _IMPORT
        | _RETURN
        | WHILE
        | BOOL
        | BUILTIN_FN
        | FN
        | FROM
        | INT
        | MAP
        | STR
        | STRUCT
        | VEC
