

// Rust enum for comparison
// source: https://doc.rust-lang.org/book/ch06-01-defining-an-enum.html

// enum Message {
//     Quit,
//     Move { x: i32, y: i32 },
//     Write(String),
//     ChangeColor(i32, i32, i32),
// }

struct coord {
    x as int,
    y as int,
}

struct empty {}

struct rgb {
    r as int,
    g as int,
    b as int,
}

enum message {
    quit as empty,
    move as coord,
    write as str,
    change_color as rgb,
}

fn is_quit_message args m as message return bool {
    m
    match {
        case message:quit { drop true }
        case message:move { drop false }
        case message:write { drop false }
        case message:change_color { drop false }
    }
}

fn main {
    // construct enum with attached values

    rgb
    dup "r" { 1 } !
    dup "g" { 2 } !
    dup "b" { 3 } !
    message:change_color
    // stack: message:change_color{1, 2, 3}
    drop

    coord
    dup "x" { 4 } !
    dup "y" { 5 } !
    message:move
    // stack: message:move{4, 5}
    drop

    "something"
    message:write
    // stack: message:written{"something"}
    drop

    empty
    message:quit
    // stack: message
    drop

    empty
    message:quit

    match { // consumes message
        case message:quit {
            // stack: struct{}
            "quit\n" .

            drop
            // stack: <empty>
        }
        case message:move {
            // stack: coord
            "move " .

            use c {
                c "x" ? .
                " " .
                c "y" ? .
            }
            "\n" .

            // stack: <empty>

        }
        case message:write {
            // stack: str
            "write " .

            use w {
                w .
            }
            "\n" .

            // stack: <empty>
        }
        case message:change_color {
            // stack: struct { r as int, g as int, y as int, }
            "color " .

            use c {
                c "r" ? .
                " " .
                c "g" ? .
                " " .
                c "b" ? .
            }
            "\n" .

            // stack: <empty>
        }
    }
    // stack: <empty>

    empty
    message:quit
    is_quit_message assert

    "something"
    message:write
    is_quit_message not assert

}
